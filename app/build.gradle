apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "de.smartsquare.kickchain"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        def properties = new Properties()
        def propertiesFile = rootProject.file("project.properties")

        if (!propertiesFile.exists()) {
            throw new GradleException("Please add a project.properties file with an endpoint property.")
        }

        propertiesFile.withInputStream { properties.load(it) }

        def endpoint = properties.get("endpoint")
        if (endpoint == null) {
            throw new GradleException("Please add an endpoint property.")
        }

        debug {
            buildConfigField "String", "ENDPOINT", endpoint
        }

        release {
            buildConfigField "String", "ENDPOINT", endpoint

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:support-compat:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'

    implementation 'com.uber.autodispose:autodispose-kotlin:0.8.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents-kotlin:0.8.0'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    implementation 'com.github.rubengees:kotterknife:1.1'
}
